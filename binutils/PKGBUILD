# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=binutils
pkgver=2.37
pkgrel=2
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=('x86_64')
url='https://www.gnu.org/software/binutils/'
license=('GPL')
groups=('base-devel')
depends=('glibc' 'zlib' 'libelf')
makedepends=('git')
checkdepends=('dejagnu' 'debuginfod' 'bc')
optdepends=('debuginfod: for debuginfod server/client functionality')
options=('staticlibs' '!distcc' '!ccache')
source=("git+https://sourceware.org/git/binutils-gdb.git#branch=binutils-${pkgver/./_}-branch")
sha256sums=('SKIP')

prepare() {
  mkdir -p binutils-build

  cd binutils-gdb

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh
}

build() {
  cd binutils-build

  "$srcdir/binutils-gdb/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --enable-cet \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-install-libiberty \
    --enable-ld=default \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-threads \
    --disable-gdb \
    --disable-gdbserver \
    --disable-libdecnumber \
    --disable-readline \
    --disable-sim \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make -O tooldir=/usr
}

check() {
  cd binutils-build

  # current testsuite failure in debuginfod (objdump)
  # https://sourceware.org/bugzilla/show_bug.cgi?id=28029
  sed -i '/test_fetch_debuglink $OBJDUMP/d' \
      $srcdir/binutils-gdb/binutils/testsuite/binutils-all/debuginfod.exp

  # Use minimal flags for testsuite
  # ld testsuite uses CFLAGS_FOR_TARGET and requires -g
  # gold testsuite requires CXXFLAGS/CFLAGS with default PIE/PIC disabled
  make -O CFLAGS_FOR_TARGET="-O2 -g" \
          CXXFLAGS="-O2 -no-pie -fno-PIC" \
          CFLAGS="-O2 -no-pie" \
          LDFLAGS="" \
          check || true
}

package() {
  cd binutils-build
  make -j1 prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
