# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc
pkgver=11.2.1
_majorver=${pkgver%%.*}
_islver=0.24
pkgrel=6
pkgdesc='The GNU Compiler Collection'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url='https://gcc.gnu.org'
depends=('binutils' 'libmpc')
makedepends=('python' 'git' 'libxcrypt')
checkdepends=('dejagnu' 'inetutils')
optdepends=('mold')
provides=("gcc-libs=$pkgver" "gcc-fortran=$pkgver")
conflicts=('gcc-libs' 'gcc-fortran')
replaces=('gcc-libs' 'gcc-fortran')
options=('!emptydirs' 'staticlibs' '!lto')
source=("git://gcc.gnu.org/git/gcc.git#branch=releases/gcc-$_majorver"
        "https://libisl.sourceforge.io/isl-${_islver}.tar.xz"
        'c89'
        'c99'
        '0001-mold.patch'
)
sha256sums=('SKIP'
            '043105cc544f416b48736fff8caf077fb0663a717d06b1113f16e391ac99ebad'
            'de48736f6e4153f03d0a5d38ceb6c6fdb7f054e8f47ddd6af0a3dbf14f27b931'
            '2513c6d9984dd0a2058557bf00f06d8d5181734e41dcfe07be7ed86f2959622a'
            '149614cb5d6c885fa30ebb0b8effe383c591221a0ba7037d938ab2aa7d1f42aa')

prepare() {
  [[ ! -d gcc ]] && ln -s gcc-${pkgver/+/-} gcc
  cd gcc

  # link isl for in-tree build
  ln -s ../isl-${_islver} isl

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # Arch Linux installs x86_64 libraries /lib
  sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64
  # Patching Mold for GCC 11.2
  patch -p1 "$srcdir/0001-mold.patch"

  mkdir -p "$srcdir/gcc-build"

}

build() {
  cd gcc-build

  # TODO: properly deal with the build/testsuite issues resulting from this
  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  # TODO: check if we can only remove these when running testsuite
  # baseline - 44 failures in gcc (on my machine...)
  CFLAGS=${CFLAGS/-fexceptions/}		# +7 failures in gcc, +14 in libitm
  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=2/}	# +25 failures in libstdc++
  CXXFLAGS="${CFLAGS}"
  #CXXFLAGS+=" -Wp,-D_GLIBCXX_ASSERTIONS" 	# +2 failures in libstdc++

  "$srcdir/gcc/configure" --prefix=/usr \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      --mandir=/usr/share/man \
      --infodir=/usr/share/info \
      --enable-languages=c,c++,fortran,lto \
      --with-isl \
      --with-linker-hash-style=gnu \
      --with-system-zlib \
      --enable-__cxa_atexit \
      --enable-cet=auto \
      --enable-checking=release \
      --enable-clocale=gnu \
      --enable-default-pie \
      --enable-default-ssp \
      --enable-gnu-indirect-function \
      --enable-gnu-unique-object \
      --enable-linker-build-id \
      --enable-plugin \
      --enable-shared \
      --enable-threads=posix \
      --disable-libssp \
      --disable-libstdcxx-pch \
      --disable-multilib \
      --disable-werror

  make -O STAGE1_CFLAGS="-O2" \
          BOOT_CFLAGS="$CFLAGS" \
          BOOT_LDFLAGS="$LDFLAGS" \
          LDFLAGS_FOR_TARGET="$LDFLAGS"
}

check() {
  cd gcc-build

  # do not abort on error as some are "expected" - gcc testsuite is brittle
  make -O -k check || true
  "$srcdir/gcc/contrib/test_summary"
}

package() {
  cd gcc-build
  make DESTDIR="$pkgdir" install

  # many packages expect these symlinks
  ln -s gcc "$pkgdir"/usr/bin/cc
  ln -s gfortran "$pkgdir/usr/bin/f95"

  # POSIX conformance launcher scripts for c89 and c99
  install -Dm755 "$srcdir/c89" "$pkgdir/usr/bin/c89"
  install -Dm755 "$srcdir/c99" "$pkgdir/usr/bin/c99"

  # byte-compile python libraries
  python -m compileall "$pkgdir/usr/share/gcc-${pkgver}/"
  python -O -m compileall "$pkgdir/usr/share/gcc-${pkgver}/"

  # Install Runtime Library Exception
  install -Dm644 "$srcdir/gcc/COPYING.RUNTIME" \
    "$pkgdir/usr/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION"
}
