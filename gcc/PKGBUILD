# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc
pkgver=11.2.1
_majorver=${pkgver%%.*}
_islver=0.24
pkgrel=8
pkgdesc='The GNU Compiler Collection'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url='https://gcc.gnu.org'
depends=('libmpc')
makedepends=('python' 'git' 'libxcrypt')
checkdepends=('dejagnu' 'inetutils' 'autogen')
provides=("gcc-libs=$pkgver" "gcc-fortran=$pkgver")
conflicts=('gcc-libs' 'gcc-fortran')
replaces=('gcc-libs' 'gcc-fortran')
options=('!emptydirs' 'staticlibs' '!lto')
source=("git+https://gcc.gnu.org/git/gcc.git#branch=releases/gcc-$_majorver"
        "https://libisl.sourceforge.io/isl-${_islver}.tar.xz"
        'c89'
        'c99'
        'fix-testsuite-default-pie.diff'
        'fix-testsuite-default-ssp.diff'
        'fix-testsuite-analyzer-glibc-2.34.diff')
sha256sums=('SKIP'
            '043105cc544f416b48736fff8caf077fb0663a717d06b1113f16e391ac99ebad'
            'de48736f6e4153f03d0a5d38ceb6c6fdb7f054e8f47ddd6af0a3dbf14f27b931'
            '2513c6d9984dd0a2058557bf00f06d8d5181734e41dcfe07be7ed86f2959622a'
            '3f2fc47ec6c67ff080f69e0ac89e71f9374e50293b9f99a2509477c3bd4787d1'
            '312239a648f1c3330638d98123526cf340fccc9ca1d8b2518c9871893507cf7e'
            '0500a18f11c0ee843b2ddff80ff2f62cde50aec60449d3d7807ed163d62025a1')

prepare() {
  [[ ! -d gcc ]] && ln -s gcc-${pkgver/+/-} gcc
  cd gcc

  # link isl for in-tree build
  ln -s ../isl-${_islver} isl

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # Arch Linux installs x86_64 libraries /lib
  sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  # TODO - submit upstream once development reopens
  patch -p1 -i "${srcdir}"/fix-testsuite-default-pie.diff

  # fix testsuite failures with default SSP
  # upstream commit 90c31ff339015ddd89ac519656fbd23a36ee6271
  patch -p1 -i "${srcdir}"/fix-testsuite-default-ssp.diff

  # fix testsuite failures with glibc-2.34+
  # https://gcc.gnu.org/pipermail/gcc-patches/2022-February/589886.html
  patch -p1 -i "${srcdir}"/fix-testsuite-analyzer-glibc-2.34.diff

  mkdir -p "$srcdir/gcc-build"
}

build() {
  cd gcc-build

  # TODO: properly deal with the build/testsuite issues resulting from this
  CFLAGS=${CFLAGS/-Werror=format-security/}
  CXXFLAGS=${CXXFLAGS/-Werror=format-security/}

  # TODO: check if we can only remove these when running testsuite
  CFLAGS=${CFLAGS/-fexceptions/}		# +7 failures in gcc, +14 in libitm
  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=2/}	# +25 failures in libstdc++
  CXXFLAGS="${CFLAGS}"
  #CXXFLAGS+=" -Wp,-D_GLIBCXX_ASSERTIONS" 	# +2 failures in libstdc++

  "$srcdir/gcc/configure" --prefix=/usr \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      --mandir=/usr/share/man \
      --infodir=/usr/share/info \
      --enable-languages=c,c++,fortran,lto \
      --with-isl \
      --with-linker-hash-style=gnu \
      --with-system-zlib \
      --enable-__cxa_atexit \
      --enable-cet=auto \
      --enable-checking=release \
      --enable-clocale=gnu \
      --enable-default-pie \
      --enable-default-ssp \
      --enable-gnu-indirect-function \
      --enable-gnu-unique-object \
      --enable-linker-build-id \
      --enable-plugin \
      --enable-shared \
      --enable-threads=posix \
      --disable-libssp \
      --disable-libstdcxx-pch \
      --disable-multilib \
      --disable-werror

  make -O STAGE1_CFLAGS="-O2" \
          BOOT_CFLAGS="$CFLAGS" \
          BOOT_LDFLAGS="$LDFLAGS" \
          LDFLAGS_FOR_TARGET="$LDFLAGS"
}

check() {
  cd gcc-build

  # do not abort on error as some are "expected" - gcc testsuite is brittle
  make -O -k check
  "$srcdir/gcc/contrib/test_summary"
}

package() {
  cd gcc-build
  make DESTDIR="$pkgdir" install

  # many packages expect these symlinks
  ln -s gcc "$pkgdir"/usr/bin/cc
  ln -s gfortran "$pkgdir/usr/bin/f95"

  # POSIX conformance launcher scripts for c89 and c99
  install -Dm755 "$srcdir/c89" "$pkgdir/usr/bin/c89"
  install -Dm755 "$srcdir/c99" "$pkgdir/usr/bin/c99"

  # byte-compile python libraries
  python -m compileall "$pkgdir/usr/share/gcc-${pkgver}/"
  python -O -m compileall "$pkgdir/usr/share/gcc-${pkgver}/"

  # Install Runtime Library Exception
  install -Dm644 "$srcdir/gcc/COPYING.RUNTIME" \
    "$pkgdir/usr/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION"
}
